apply plugin: 'com.android.application'
apply plugin: 'com.github.gfx.ribbonizer'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'

ext {
    ciBuild = System.getenv("CI") == "true"
    travisBuild = System.getenv("TRAVIS") == "true"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "jp.gcreate.product.filteredhatebu"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 7
        versionName "v16.12.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    productFlavors {
        mock {
            applicationIdSuffix ".mock"
        }
        prod {
        }
    }
    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            // Travisでだけリリースビルドを行う
            if (travisBuild) {
                storeFile rootProject.file('release.keystore')
                storePassword System.getenv('filtered_hatebu_pass')
                keyAlias System.getenv('filtered_hatebu_alias')
                keyPassword System.getenv('filtered_hatebu_key_pass')
            }
            // ローカルビルド
            if (!ciBuild) {
                storeFile rootProject.file('release.keystore')
                storePassword getProperty('filtered_hatebu_pass')
                keyAlias getProperty('filtered_hatebu_alias')
                keyPassword getProperty('filtered_hatebu_key_pass')
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        preDexLibraries = !ciBuild
    }
    sourceSets {
        mock {
            // androidTest用のmock用ファイルをJunit Testでも利用するための設定
            // getClass().getClassLoader().getResources()でアクセスできるようにする
            test.resources.srcDirs += "src/mock/assets"
        }
    }
}

ribbonizer {
    builder { variant, iconFile ->
        if (variant.flavorName == "mock") {
            return yellowRibbonFilter(variant, iconFile)
        } else {
            return greenRibbonFilter(variant, iconFile)
        }
    }

    forcedVariantsNames "mockRelease"
}

play {
    track = 'production'
    if (!ciBuild) {
        jsonFile = rootProject.file("filteredhatebu_play_api.json")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def support_ver = "25.1.0"
    compile "com.android.support:appcompat-v7:$support_ver"
    compile "com.android.support:design:$support_ver"
    compile "com.android.support:recyclerview-v7:$support_ver"
    compile "com.android.support:cardview-v7:$support_ver"
    compile "com.android.support:customtabs:$support_ver"

    compile 'com.google.dagger:dagger:2.7'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.7'

    compile 'com.jakewharton.timber:timber:4.3.1'
    compile 'io.reactivex:rxjava:1.2.2'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'

    def orma_version = '3.0.0'
    annotationProcessor "com.github.gfx.android.orma:orma-processor:$orma_version"
    compile "com.github.gfx.android.orma:orma:$orma_version"

    // json
    compile 'com.squareup.moshi:moshi:1.2.0'
    compile 'com.google.code.gson:gson:2.7:'

    // network
    def okhttp_ver = "3.4.1"
    compile "com.squareup.okhttp3:okhttp:$okhttp_ver"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp_ver"

    debugCompile 'com.facebook.stetho:stetho:1.4.1'

    def leak_ver = '1.5'
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leak_ver"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_ver"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_ver"
    androidTestCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_ver"

    def retrofit_ver = "2.1.0"
    compile "com.squareup.retrofit2:retrofit:$retrofit_ver"
    compile "com.squareup.retrofit2:converter-moshi:$retrofit_ver"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_ver"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_ver"
    compile("com.squareup.retrofit2:converter-simplexml:$retrofit_ver") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    def junit4 = 'junit:junit:4.12'
    def espresso_ver = '2.2.2'
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso_ver", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espresso_ver", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile junit4
    testCompile junit4
    testCompile "org.robolectric:robolectric:3.1.2"
    testCompile 'org.mockito:mockito-core:1.10.19'
}

/*
 * On Travis-CI androidTest failed as java.lang.IllegalAccessError: Class ref in pre-verified class resolved to unexpected implementation
 * at android.databinding.DataBindingUtil.<clinit>(DataBindingUtil.java:31)
 *
 * This is workaround for it.
 *
 * Workaround for https://code.google.com/p/android/issues/detail?id=182715
 *
 * The Android Gradle plugin is creating DataBindingExportBuildInfoTasks for the instrumentation
 * APK that generates from the app APKs layouts. This creates duplicate classes in the app and
 * instrumentation APK which leads to
 *      java.lang.IllegalAccessError: Class ref in pre-verified class resolved to unexpected implementation
 * on older devices.
 *
 * The workaround is to get the DataBindingExportBuildInfoTasks tasks for the instrumentation APK
 * and delete the files right after it creates them.
 */
tasks.withType(com.android.build.gradle.internal.tasks.databinding.DataBindingExportBuildInfoTask) { task ->
    if (task.name.endsWith("AndroidTest")) {
        task.finalizedBy(tasks.create("${task.name}Workaround") << {
            task.output.deleteDir()
        })
    }
}