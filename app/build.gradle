apply plugin: 'com.android.application'
apply plugin: 'com.github.gfx.ribbonizer'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'

android {
    apply from: "${System.getenv('HOME')}/.android-mykey/FilteredHatebu.sign.gradle", to: android

    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "jp.gcreate.product.filteredhatebu"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 4
        versionName "v16.10.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    productFlavors {
        mock {
            applicationIdSuffix ".mock"
        }
        prod {
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dataBinding {
        enabled = true
    }
}

ribbonizer {
    builder { variant, iconFile ->
        if (variant.flavorName == "mock") {
            return yellowRibbonFilter(variant, iconFile)
        } else {
            return greenRibbonFilter(variant, iconFile)
        }
    }

    forcedVariantsNames "mockRelease"
}

play {
    track = 'production'
    jsonFile = file(System.getenv('HOME') + "/.android-mykey/filteredhatebu_play_api.json")
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def support_ver = "25.0.0"
    compile "com.android.support:appcompat-v7:$support_ver"
    compile "com.android.support:design:$support_ver"
    compile "com.android.support:recyclerview-v7:$support_ver"
    compile "com.android.support:cardview-v7:$support_ver"
    compile "com.android.support:customtabs:$support_ver"

    compile 'com.google.dagger:dagger:2.7'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.7'

    compile 'com.jakewharton.timber:timber:4.3.1'
    compile 'io.reactivex:rxjava:1.2.1'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'

    def orma_version = '3.0.0'
    annotationProcessor "com.github.gfx.android.orma:orma-processor:$orma_version"
    compile "com.github.gfx.android.orma:orma:$orma_version"

    // json
    compile 'com.squareup.moshi:moshi:1.2.0'
    compile 'com.google.code.gson:gson:2.7:'

    // network
    def okhttp_ver = "3.4.1"
    compile "com.squareup.okhttp3:okhttp:$okhttp_ver"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp_ver"

    debugCompile 'com.facebook.stetho:stetho:1.4.1'

    def leak_ver = '1.5'
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leak_ver"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_ver"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_ver"
    androidTestCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_ver"

    def retrofit_ver = "2.1.0"
    compile "com.squareup.retrofit2:retrofit:$retrofit_ver"
    compile "com.squareup.retrofit2:converter-moshi:$retrofit_ver"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_ver"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_ver"
    compile("com.squareup.retrofit2:converter-simplexml:$retrofit_ver") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    def junit4 = 'junit:junit:4.12'
    def espresso_ver = '2.2.2'
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso_ver", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espresso_ver", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile junit4
    testCompile junit4
    testCompile "org.robolectric:robolectric:3.1.2"
    testCompile 'org.mockito:mockito-core:1.10.19'
}
